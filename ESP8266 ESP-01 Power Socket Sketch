#include <ESP8266WiFi.h>            
#include <ESP8266WebServer.h>
#include <ESP8266HTTPClient.h>

uint8_t PowerPin = 2;     //GPIO of relay
bool    PowerOn  = false; //current state of relay

const String nodeId = "4";
const char* ssid = "Scirocco_Home";
const char* password = "likenoother";
const String serverURL = "http://192.168.1.129:8181";
const String aliveRequestURL = "/core-rest-api/for-nodes/alive-request-from-node";

ESP8266WebServer server(80);   //Web server object. Will be listening in port 80 (default for HTTP)

void setup() {
  // Initializing the relay control port
  pinMode(PowerPin , OUTPUT);
  digitalWrite(PowerPin , PowerOn);
  
  WiFi.begin(ssid, password); //Connect to the WiFi network

  while (WiFi.status() != WL_CONNECTED) { //Wait for connection
    delay(500);
  }

  sendAliveRequestToServer();
  
  server.on("/command-from-server", handleRequestFromServer); //Associate the handler function to the path
  server.begin();                                    //Start the server
}

void loop() {
  server.handleClient();    //Handling of incoming requests
}

void handleRequestFromServer() {
  String message = "";

  if (server.arg("switch")== ""){     //Parameter not found
    message = "Switch argument not found.";
  } else {     //Parameter found
    if(powerSocketSwitch(server.arg("switch")) == true){
      message = "Switched successfully.";
    } else {
      message = "Switched unsuccessfully. Command is not correct.";
    }    
  }

  server.send(200, "text/plain", message);    //Returns the HTTP response
}

boolean powerSocketSwitch(String command){
  if(command == "unchecked"){
    PowerOn = false;
    digitalWrite(PowerPin , PowerOn);
    return true;
  } else if(command == "checked"){
    PowerOn = true;
    digitalWrite(PowerPin , PowerOn);
    return true;
  }
  return false;
}

void sendAliveRequestToServer() { 
  if (WiFi.status() == WL_CONNECTED) { //Check WiFi connection status
    
    HTTPClient http;  //Declare an object of class HTTPClient
    http.begin(serverURL + aliveRequestURL + "?id=" + nodeId);  //Specify request destination
    int httpCode = http.GET();                                  //Send the request
 
    if (httpCode > 0) { //Check the returning code
      String payload = http.getString();   //Get the request response payload
    }
    http.end();   //Close connection
  }
}
